    10 REM SECTOR DUMP TOOL FOR ND80
    20 REM COPYRIGHT(C) 2018
    30 REM    BY TETSUO ODA AKA ODAMAN
    40 REM    odaman68k.amiga@gmail.com
    50 REM [USAGE]
    60 REM J - HEXT PAGE, K - PREV PAGE
    70 REM L - NEXT SEC., H - PREV SEC.
    80 REM S - INPUT SEC.#
    90 REM
   100 REM 8004-8225 (Code+Workarea)
   110 REM 8226-     (BASIC)
   120 REM
   130 CLS :GOSUB *SM
   140 L%=23:W%=8:B%=L%*W%:D%=$8025
   150 IF PEEK($F150)=80 THEN W%=16
   160 LOCATE 0,0:PRINT SPACE$(30)
   170 LOCATE 0,0:INPUT "SECTOR#"S
   180 IF S<0 THEN S=0
   190 S=INT(S):O%=0:GOSUB *CP
   200 GOSUB *DP
   210 K$=INKEY$
   220 IF K$=" "THEN GOTO 210
   230 IF K$="S"THEN GOTO 160
   240 IF K$="J"THEN O%=O%+B%:GOTO 290
   250 IF K$="K"THEN O%=O%-B%:GOTO 290
   260 IF K$="H"THEN S=S-1:GOTO 180
   270 IF K$="L"THEN S=S+1:GOTO 180
   280 GOTO 210
   290 IF O%<0 THEN O%=0
   300 IF O%>(512-B%)THEN O%=512-B%
   310 GOTO 200
   320 *DP
   330 LOCATE 0,0
   340 PRINT "SECTOR#";S
   350 P%=O%:FOR Y%=0 TO L%-1
   360 PRINT HEX$(P%,3);": ";
   370 FOR X%=0 TO W%-1
   380 C%=PEEK(D%+P%+X%)
   390 PRINT HEX$(C%,2);" ";:NEXT X%
   400 PRINT ":";:FOR X%=0 TO W%-1
   410 C%=PEEK(D%+P%+X%):C$=CHR$(C%)
   420 IF C%<32 THEN C$="."
   430 PRINT C$;:NEXT X%:P%=P%+W%
   440 PRINT :NEXT Y%:RETURN 
   450 *CP
   460 S0=INT(S/65536):SW=S-S0*65536
   470 S1=INT(SW/256):S2=SW-S1*256
   480 POKE $8004,S0:POKE $8005,S1
   490 POKE $8006,S2:USR($8007):RETURN 
   500 *SM
   510 FOR I%=0 TO 32
   520 READ D$:H$="$"+D$:D%=VAL(H$)
   530 POKE I%+$8004,D%:NEXT I%:RETURN 
   540 DATA 00,00,00,2A,04,80,45,4C,3A
   550 DATA 06,80,21,25,80,CD,24,70,C9
   560 DATA 2A,04,80,45,4C,3A,06,80,21
   570 DATA 25,80,CD,27,70,C9
