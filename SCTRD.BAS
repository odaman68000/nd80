    10 REM SECTOR DUMP TOOL FOR ND80
    20 REM COPYRIGHT(C) 2018
    30 REM BY TETSUO ODA AKA ODAMAN
    40 REM [USAGE]
    50 REM J - HEXT PAGE, K - PREV PAGE
    60 REM L - NEXT SEC., H - PREV SEC.
    65 REM S - INPUT SEC.#
    70 REM
    71 REM 8004-8225 (Code+Workarea)
    72 REM 8226-     (BASIC)
    73 REM
    80 CLS :GOSUB *SM
    90 L%=23:W%=8:B%=L%*W%:D%=$8025
   100 LOCATE 0,0:PRINT SPACE$(30)
   110 LOCATE 0,0:INPUT "SECTOR#"S
   111 IF S<0 THEN S=0
   120 S=INT(S):O%=0:GOSUB *CP
   130 GOSUB *DP
   140 K$=INKEY$
   150 IF K$=" "THEN GOTO 140
   160 IF K$="S"THEN GOTO 100
   170 IF K$="J"THEN O%=O%+B%:GOTO 220
   180 IF K$="K"THEN O%=O%-B%:GOTO 220
   190 IF K$="H"THEN S=S-1:GOTO 111
   200 IF K$="L"THEN S=S+1:GOTO 111
   210 GOTO 140
   220 IF O%<0 THEN O%=0
   230 IF O%>(512-B%)THEN O%=512-B%
   240 GOTO 130
   250 *DP
   251 LOCATE 0,0
   260 PRINT "SECTOR#";S;
   261 PRINT " ($";HEX$(S,6);")"
   270 P%=O%:FOR Y%=0 TO L%-1
   290 PRINT HEX$(P%,3);": ";
   300 FOR X%=0 TO W%-1
   310 C%=PEEK(D%+P%+X%)
   320 PRINT HEX$(C%,2);" ";:NEXT X%
   330 PRINT ":";:FOR X%=0 TO W%-1
   340 C%=PEEK(D%+P%+X%):C$=CHR$(C%)
   350 IF C%<32 THEN C$="."
   360 PRINT C$;:NEXT X%:P%=P%+W%
   370 PRINT :NEXT Y%:RETURN 
   380 *CP
   381 S0=INT(S/65536):SW=S-S0*65536
   382 S1=INT(SW/256):S2=SW-S1*256
   390 POKE $8004,S0:POKE $8005,S1
   400 POKE $8006,S2:USR($8007):RETURN 
   430 *SM
   440 FOR I%=0 TO 32
   450 READ D$:H$="$"+D$:D%=VAL(H$)
   460 POKE I%+$8004,D%:NEXT I%:RETURN 
   470 DATA 00,00,00,2A,04,80,45,4C,3A
   480 DATA 06,80,21,25,80,CD,24,70,C9
   490 DATA 2A,04,80,45,4C,3A,06,80,21
   500 DATA 25,80,CD,27,70,C9
